= element Cilium.  A sentry and sensory extracellular assistant
\symbol Ci
\color #833
\symmetries all

== Data members
u Bool mPullRes = true;

== Rules
given @ isa Cilium

# Shutdown if too crowded (indirectly consuming excess Res)

vote C : $curatom is Res || $curatom is Cilium
check C : $nvotes > 5u

   CCC    ...
   C@C -> ._.
   CCC    ...


# Push DReg

vote o isa OuterMembrane
vote d isa DReg

change d { ew.swap($_.$winsn,$d.$winsn); }

 _ddd@oooo  -> .d.......

 _          .
  d          d
   @oo   ->   ...
   ooo        ...
   ooo        ...

    @oo  ->   ...
   _doo      d...

# Shutdown if isolated

given n : !($curatom is OuterMembrane)

      n                .
      n                .
      n                .
      n                .
  nnnn@nnnn  ->    ...._....
      n                .
      n                .
      n                .
      n                .

== Rules (Pull res)
given @ isa Cilium : $self.mPullRes

vote o isa OuterMembrane

vote r isa Res
change r { ew.swap($_.$winsn,$r.$winsn); }

 rrr@_oo    -> ....r..


 rrr_@oooo  -> .r.......

 r          .
  _          r
   @oo   ->   ...
   ooo        ...
   ooo        ...


# Swap Res in if we're too close anyway
vote R isa Res

  R@o  -> @R.       
                    
== Rules (Clear the way)
given @ isa Cilium : !$self.mPullRes
                        
vote e isa Empty        
vote o isa OuterMembrane
                        
vote x : !($curatom is QMembrane)   // But might be Cilium!
change x { ew.swap($e.$winsn,$x.$winsn); }
                            
    e          x     
    @xoo  ->   ....  
    e          .     
                            
    x           .    
   e@oo   ->   x...  
    x           .    
                     
   _@oo   ->   @_..
             
== Rules (Diffuse near OM)
given @ isa Cilium
                    
vote o isa OuterMembrane
vote n : !($curatom is OuterMembrane)

vote e isa Empty    
change e { ew.swap($e.$winsn,$@.$winsn); }

   @     ->  .
   enoo      e...

   e@oo  ->  e...

  @enoo  ->  .e...

== Rules (Hodl)
given @ isa Cilium

  @ -> .
