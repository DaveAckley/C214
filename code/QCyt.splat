= quark QCyt isa QContent.  Base class for hard/soft 'cytoplasmic jelly'
\symmetries all

== Data members

u Bool mDie = false;

== Methods

== Prerules (Death propagation)
given @ isa QCyt : $self.mDie

given q : true                       // Non-existent sites match too
vote q isa QCyt : $self.mDie = true  // Drive-by spreading the bad news
check q : true                       // But we're decaying in any case

change R isa Res
 
     q             .
    qqq           ...
   qqqqq         .....
  qqqqqqq       .......
 qqqq@qqqq ->  ....R....
  qqqqqqq       .......
   qqqqq         .....
    qqq           ...
     q             .


== Rules (Fatal conditions)

# We should never be adjacent to OuterMembrane
vote o isa OuterMembrane
change @ isa QCyt { $self.mDie = true; }

 ooo    ...
 o@o -> .@.
 ooo    ...


== Rules (Seed gradient)
given i isa InnerMembrane : $self.mGradVal == $self.mGradVal.maxof
check @ : random.oneIn(1000)
change i isa InnerMembrane { $self.mGradVal = $self.mGradVal.minof; }

 i@ -> i.

== Rules (Growth)

given @ isa QCyt
given R isa Res

  @.R -> @.@

# == Rules (Maturity)
# # Try to complete crystallization

# given Q isa QCyt
# given N : ! ($curatom is QCyt)
# given X : ! ($curatom is QMembrane)

#     Q@X -> .X@    # A bit too close, back off

#   Q.X@.NN -> ..@X...  # Too far, not losing anybody

#     QX  -> .@     # This is just weird so change it
#      @      X
#      .      .
#      N      .
#      N      .


#    Q.X     ..@
#      @  ->   X    # This too
#      .       .
#      N       .
#      N       .

#    Q_@  ->  ...   # This is what happy means to me
