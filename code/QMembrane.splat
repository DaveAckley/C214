#####
= quark QMembrane. The parent class of inner and outer membrane
\symmetries all

== Data members

== Rules: OM management Part 1 (miscellaneous business)
given @ isa OuterMembrane
given i isa InnerMembrane
given o isa OuterMembrane
given q isa QMembrane
given n : !($curatom is InnerMembrane)

# Fight DReg
vote D isa DReg
vote X { 
.  return ($curatom is DReg             // Swapping this inward doesn't help
.          || $curatom is QMembrane)    // and moving this could be disruptive
.       ? 0u                            // So don't consider them 
.       : (Votes) ew.getCoord($cursn).euclideanSquaredLength(); // Otherwise, the farther the better
. }

change D { ew.swap($X.$winsn,$D.$winsn); }

    XD      ..
   XXD@ -> D...
    XD      ..

# Import
vote C isa QContent
check R {
.  DebugUtils du;
.//  du.print("CheckR");
.//  du.print($C.$nvotes);
.//  du.print($R.$nvotes);
.  if ($C.$nvotes == 0u || $R.$nvotes == 0u) return false;
.//  du.print("CheckR2");
.//  du.print($C.$winsn);
.//  du.print($R.$winsn);
.  QContent & qc = (QContent &) ew[$C.$winsn];
.//  du.print(qc);
.//  du.print(ew[$R.$winsn]);
.  return qc.shouldImport(ew[$R.$winsn]);
.}
#change R { ew.swap($R.$winsn,$cursn); }

    C       .
 R@i_ -> _..R
    C       .

# Self-stabilization rules

  i_       .@
  i@   ->  ..    # Square off (complete outer membrane)

  nnn      ...
  n@n  ->  ._.   # Die off (eliminate isolated outer membrane)
  nnn      ...

  iii      ...
  i@i  ->  .i.   # Turn in (eliminate surrounded outer membrane)
  iii      ...

== Rules: OM management Part 2 (mostly growth)

given @ isa OuterMembrane
given i isa InnerMembrane
given o isa OuterMembrane
given q isa QMembrane

given a isa OuterMembrane
given b isa OuterMembrane
given c isa InnerMembrane

given e : true      // Allow dead sites
vote  e isa Empty   // Count empty sites
check e : $nvotes*3u>=$nsites*2u

vote  f : ($curatom is QContent) || ($curatom is Res) || ($curatom is QX2) // Count up legit content
check f : random.oddsOf($nvotes,3)

 ee_oqff     ..@....
 eeo@iff ->  ...c...     # Run out
 eeoicff     .......

    eo        ..
    _aif      a...
    _@cf  ->  @c..       # Break out
    _bif      b...
    eo        ..

      ff       ..
    qiif     ....
    o@cf  -> .c..        # Punch out
    qiif     ....
      ff       ..

vote r : $curatom is Empty
.     || $curatom is OuterMembrane

    roqf          o...
    o@if    ->    .c..   # Cave out
    qicf          ....
    ffff          ....

# OuterMembrane topology changing rules

   fffff          .....
  iiiciii        .......
 oooo@oooo  ->  ....c....  # Fusion
  iiiciii        .......
   fffff          .....


== Rules: IM management (tail shrinking)
given i isa InnerMembrane
given o isa OuterMembrane

given @ isa InnerMembrane
check @ : ($o.$nsites >= 7u) && ($i.$nsites <= 1u)

let x = i|o

  xxx      ...
  x@x  ->  .o.      # Shrink tail
  xxx      ...

== Rules: Export
given o isa OuterMembrane

vote C isa QContent
check R {
.  if ($C.$nvotes == 0u || $R.$nvotes == 0u) return false;
.  QContent & qc = (QContent &) ew[$C.$winsn];
.  return qc.shouldExport(ew[$R.$winsn]);
.}

    C       .
 _@oR -> R.._
    C       .

== Rules: IM management (shrink and die)
given @ isa InnerMembrane
given i isa InnerMembrane
given a isa InnerMembrane
given b isa InnerMembrane
given c isa OuterMembrane
given d isa OuterMembrane

given o isa OuterMembrane
let x = i|o
given q isa QMembrane
given n : !($curatom is OuterMembrane)

vote  e isa QContent
check e : $nvotes == 0u // Say no QContent

given f : true   // Allow dead sites
vote  f : !($curatom is Empty)
check f : random.oddsOf(3u*$nvotes+1u,10u)

# Self-stabilization rules

  ___      ...
  o@o  ->  .o.      # Cap off
  oxo      ...

  o_       .@
  o@   ->  ..       # Square in

  nnn      ...
  n@n  ->  ._.      # Die in
  nnn      ...

 _____     .....
 __oo_     .o...
  o@o  ->   ...     # Put your cap on

  ooo      ...
  o@o  ->  .o.      # Turn out
  ooo      ...

given z : !ew.isLive($cursn)

  o      .
  @  ->  o        # Scrub universe walls
  z      .



# Shrink rules
 ee_iqff     ..@....
 eei@off ->  ...o...     # Run in
 eeiocff     .......

   ei        ..
   _aof      a...
   _@cf  ->  @c..        # Break in
   _bof      b...
   ei        ..

      ff       ..
    qoof     ....
    i@cf  -> .c..        # Punch in
    qoof     ....
      ff       ..

vote r : $curatom is Empty
.     || $curatom is InnerMembrane

   riqf          @...
   i@of    ->    .c..     # Cave in
   qocf          ....
   ffff          ....

# InnerMembrane topology changing rules

    _____          .....
   ooocooo        .......
  iiii@iiii  ->  ....c....  # Fission
   ooocooo        .......
    _____          .....

== Postrules: Last ditch hold

  @ -> .
