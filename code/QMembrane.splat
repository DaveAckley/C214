#####
= quark QMembrane. The parent class of inner and outer membrane
\symmetries all

== Data members

== Rules: OM management Part 1 (miscellaneous business)
given @ isa OuterMembrane
given i isa InnerMembrane
given o isa OuterMembrane
given q isa QMembrane
given n : !($curatom is InnerMembrane)

# Fight DReg
vote D isa DReg
vote X {
.  return ($curatom is DReg             // Swapping DReg inward doesn't help
.          || $curatom is QMembrane)    // and moving membrane could be disruptive
.       ? 0u                            // So no votes for them
.       : (Votes) ew.getCoord($cursn).euclideanSquaredLength(); // Otherwise, the farther the better
.}

change D { ew.swap($X.$winsn,$D.$winsn); }

    XD      ..
   XXD@ -> D...
    XD      ..

# Deploy cilia
given Z : !($curatom is Cilium || $curatom is QMembrane)
vote Z isa Res               
change Z { ew[$Z.$winsn] = Cilium.instanceof; }
                             
   ZZZ     ...               
   ZZZ@ -> Z...              
   ZZZ     ...               
                             
# Program cilia (drive-by)   
scratch 0 = -1               
vote H isa HardCyt {         
.  if ($self.mRole > $0) $0 = $self.mRole;
.  return 0u;                
.}                           
                             
  H      .                   
  Hi@ -> ...                 
  H      .                   
                             
vote M isa Cilium {          
.  if ($0 >= 0u) $self.mPullRes = $0 < 13;
.  return 0u;
.}               
                 
    @MMM -> ....


# Import/export
vote C isa QContent

check m {
.  Bool m = $m.$winatom is Empty;
.  if ($C.$nvotes == 0u) {   // No content to ask
.    InnerMembrane & im = (InnerMembrane&) $i.$winatom;
.    // Default rule: If IM is worried and we'd eject a non-QC, do it
.    return im.isWorried() && m && !($x.$winatom is QContent);
.  }
.  Bool x = $x.$winatom is Empty;
.  if (m && x) return false;           // Nothing to do
.  QContent & qc = (QContent &) ew[$C.$winsn];
.  m = m || qc.shouldImport($m.$winatom); // Empty or importable
.  x = x || qc.shouldExport($x.$winatom); // Empty or exportable
.  return m && x;
.}

    CC       ..
 m@ixC -> x..m.
    CC       ..

# Self-stabilization rules

  i_       .@
  i@   ->  ..    # Square off (complete outer membrane)

  _i       i.
  i@   ->  ..    # Square corner (complete inner membrane)

  nnn      ...
  n@n  ->  ._.   # Die off (eliminate isolated outer membrane)
  nnn      ...

  iii      ...
  i@i  ->  .i.   # Turn in (eliminate surrounded outer membrane)
  iii      ...

== Rules: OM management Part 2 (mostly growth)

given @ isa OuterMembrane
given i isa InnerMembrane
given o isa OuterMembrane
given q isa QMembrane

given a isa OuterMembrane
given b isa OuterMembrane
given c isa InnerMembrane

given e : true      // Allow dead sites
vote  e : $curatom is Empty || $curatom is Cilium // Count cilia as empty!
check e : $nvotes*3u>=$nsites*2u

vote  f : ($curatom is QContent) || ($curatom is Res)   // Count up legit content
check f : random.oddsOf($nvotes,3)

 ee_oqff     ..@....
 eeo@iff ->  ...c...     # Run out
 eeoicff     .......

    eo        ..
    _aif      a...
    _@cf  ->  @c..       # Break out
    _bif      b...
    eo        ..

      ff       ..
    qiif     ....
    o@cf  -> .c..        # Punch out
    qiif     ....
      ff       ..

vote r : $curatom is Empty
.     || $curatom is OuterMembrane

    roqf          o...
    o@if    ->    .c..   # Cave out
    qicf          ....
    ffff          ....

# OuterMembrane topology changing rules

   fffff          .....
  iiiciii        .......
 oooo@oooo  ->  ....c....  # Fusion
  iiiciii        .......
   fffff          .....


== Rules: IM management (tail shrinking)
given i isa InnerMembrane
given o isa OuterMembrane

given @ isa InnerMembrane
check @ : ($o.$nsites >= 7u) && ($i.$nsites <= 1u)

let x = i|o

  xxx      ...
  x@x  ->  .o.      # Shrink tail
  xxx      ...


== Rules: IM management (shrink and die)
given @ isa InnerMembrane
given i isa InnerMembrane
given a isa InnerMembrane
given b isa InnerMembrane
given c isa OuterMembrane
given d isa OuterMembrane

given o isa OuterMembrane
let x = i|o
given q isa QMembrane
given n : !($curatom is OuterMembrane)

vote  e isa QContent
check e : $nvotes == 0u // Say no QContent

given f : true   // Allow dead sites
vote  f : !($curatom is Empty)
check f : random.oddsOf(3u*$nvotes+1u,10u)

# Self-stabilization rules

  ___      ...
  c@c  ->  .c.      # Cap off
  oxo      ...

  o_       .@
  o@   ->  ..       # Square in (complete inner membrane)

  _o       o.
  o@   ->  ..       # Square corner (complete outer membrane)

  nnn      ...
  n@n  ->  ._.      # Die in
  nnn      ...

 _____     .....
 __co_     .c...
  c@o  ->   ...     # Put your cap on

  oco      ...
  c@c  ->  .c.      # Turn out
  oco      ...

given z : !ew.isLive($cursn)

  o      .
  @  ->  o        # Scrub universe walls
  z      .



# Shrink rules
 ee_iqff     ..@....
 eei@off ->  ...o...     # Run in
 eeiocff     .......

   ei        ..
   _aof      a...
   _@cf  ->  @c..        # Break in
   _bof      b...
   ei        ..

      ff       ..
    qoof     ....
    i@cf  -> .c..        # Punch in
    qoof     ....
      ff       ..

vote r : $curatom is Empty
.     || $curatom is InnerMembrane

   riqf          @...
   i@of    ->    .c..     # Cave in
   qocf          ....
   ffff          ....

# InnerMembrane topology changing rules

    _____          .....
   ooocooo        .......
  iiii@iiii  ->  ....c....  # Fission
   ooocooo        .......
    _____          .....

== Postrules: Last ditch hold

  @ -> .
